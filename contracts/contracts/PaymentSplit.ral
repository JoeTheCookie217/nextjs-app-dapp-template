// no constructor (check on parameters)
// unknnown size array

Contract PaymentSplit(
    numPayees: U256,
    mut owner: Address,
    mut paused: Bool,
    mut numPayeesRegistered: U256,
    mut totalShares: U256,
    mut totalReleased: U256,
    mut balance: U256
) {

    mapping[Address, U256] sharesMap
    mapping[Address, U256] releasedMap

    event PayeeAdded(account: Address,  shares: U256)
    event PaymentReleased(to: Address,  amount: U256)
    event PaymentReceived(from: Address,  amount: U256)

    enum ErrorCodes {
        // InvalidPayee = 0
        InvalidShares = 1
        AccountHasShares = 2
        AccountHasNoShares = 3
        AccountIsNotDuePayment = 4
        InvalidSetup = 5
        NotAuthorized = 6
        Paused = 7
    }

    // address[] public payees

    // PUBLIC FUNCTIONS

    @using(preapprovedAssets = true, updateFields = true, checkExternalCaller = false,assetsInContract = true)
    pub fn receive(amount: U256) -> () {
        assert!(numPayeesRegistered == numPayees, ErrorCodes.InvalidSetup)
        balance = balance + amount
        transferTokenToSelf!(callerAddress!(), ALPH, amount)
        emit PaymentReceived(callerAddress!(), dustAmount!())
    }

    @using(updateFields = true, preapprovedAssets = true)
    pub fn addPayee(account: Address, shares: U256) -> () {
        // assert!(account != new Address(""), ErrorCodes.InvalidPayee)
        assert!(shares > 0, ErrorCodes.InvalidShares)
        assert!(sharesMap.contains!(account) == false, ErrorCodes.AccountHasShares)
        checkCaller!(callerAddress!() == owner, ErrorCodes.NotAuthorized)

        // payees.push(account)
        let depositor = callerAddress!()
        sharesMap.insert!(depositor, account, shares)
        emit Debug(`before: ${shares}, ${totalShares}`)
        totalShares = totalShares + shares
        emit Debug(`after: ${shares}, ${totalShares}`)
        numPayeesRegistered = numPayeesRegistered + 1
        emit PayeeAdded(account, shares)
    }

    // @using(updateFields = true, preapprovedAssets = true)
    // fn addPayeeInternal(account: Address, shares: U256) -> () {
    // }

    @using(updateFields = true, checkExternalCaller = false, assetsInContract = true)
    pub fn release(account: Address) -> () {
        assert!(!paused, ErrorCodes.Paused)
        assert!(sharesMap[account] > 0, ErrorCodes.AccountHasNoShares)
        let payment = releasable(account)
        assert!(payment != 0, ErrorCodes.AccountIsNotDuePayment)
        totalReleased = totalReleased + payment
        releasedMap[account] = releasedMap[account] + payment
        transferTokenFromSelf!(account, ALPH, payment)
        balance = balance - payment
        emit PaymentReleased(account, payment)
    }

    // VIEW FUNCTIONS

    @using(checkExternalCaller = false)
    pub fn releasable(account: Address) -> (U256) {
        let totalReceived = balance + totalReleased
        return pendingPayment(account, totalReceived, releasedMap[account])
    }

    pub fn pendingPayment(
        account: Address,
        totalReceived: U256,
        alreadyReleased: U256
    ) -> (U256) {
        return (totalReceived * sharesMap[account]) / totalShares - alreadyReleased
    }

    // ADMIN FUNCTIONS

    @using(updateFields = true)
    pub fn updateShares(account: Address, shares: U256) -> () {
        checkCaller!(callerAddress!() == owner, ErrorCodes.NotAuthorized)

        let currentShares = sharesMap[account]
        totalShares = totalShares - currentShares + shares
        if (shares == 0) {
            numPayeesRegistered = numPayeesRegistered - 1
            sharesMap.remove!(owner, account)
        }
        else {sharesMap[account] = shares}
    }

    @using(updateFields = true)
    pub fn updateOwner(newOwner: Address) -> () {
        checkCaller!(callerAddress!() == owner, ErrorCodes.NotAuthorized)
        owner = newOwner
    }

    @using(updateFields = true)
    pub fn pause() -> () {
        checkCaller!(callerAddress!() == owner, ErrorCodes.NotAuthorized)
        paused = true
    }

    @using(updateFields = true)
    pub fn unpause() -> () {
        checkCaller!(callerAddress!() == owner, ErrorCodes.NotAuthorized)
        paused = false
    }

    // HELPERS (Solidity modifiers like)

    // fn checkParams() -> () {
    //     assert!(numPayees > 0, ErrorCodes.InvalidSetup)
    // }
}